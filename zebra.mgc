dom [人]      {[丹麦人], [德国人], [挪威人], [瑞典人], [英国人]};
dom [房]      {[红房], [黄房], [绿房], [蓝房], [白房]};
dom [位置]    {[1], [2], [3], [4], [5]};
dom [宠物]    {[猫], [马], [狗], [鱼], [鸟]};
dom [饮料]    {[茶], [咖啡], [开水], [牛奶], [啤酒]};
dom [烟]      {[Ble], [Blu], [Dun], [Pal], [Pri]};

predi [在] tnum 2;
predi [住] tnum 2;
predi [喝] tnum 2;
predi [养] tnum 2;
predi [抽] tnum 2;

predi [相邻] tnum 2;

match [房] [位置] into [在];
match [人] [位置] into [在];
match [人] [宠物] into [养];
match [人] [饮料] into [喝];
match [人] [房] into [住];
match [人] [烟] into [抽];

know{
    forall [r] into [人] 
    forall [f] into [房]
    forall [j] into [位置]
    (([在]([r], [j]) and [在]([f], [j])) -> [住]([r], [f]));
}

know{ //1,2,3,9,13
    [住]([英国人], [红房]);
    [养]([瑞典人], [狗]);
    [喝]([丹麦人], [茶]);
    [在]([挪威人], [1]);
    [抽]([德国人], [Pri]);
}

know{ //4
    not [在]([白房],[1]);
    [在]([白房],[2]) == [在]([绿房],[1]);
    [在]([白房],[3]) == [在]([绿房],[2]);
    [在]([白房],[4]) == [在]([绿房],[3]);
    [在]([白房],[5]) == [在]([绿房],[4]);
}

know{ //5,6,7
    forall [i] into [人] ([住]([i], [绿房]) == [喝]([i], [咖啡]));
    forall [i] into [人] ([抽]([i], [Pal]) == [养]([i], [鸟]));
    forall [i] into [人] ([住]([i], [黄房]) == [抽]([i], [Dun]));
}

know{
    forall [i] into [人]
    forall [j] into [人]
    ((([在]([i], [1]) and [在]([j], [2])) or 
    ([在]([i], [2]) and [在]([j], [1])) or 
    ([在]([i], [2]) and [在]([j], [3])) or 
    ([在]([i], [3]) and [在]([j], [2])) or 
    ([在]([i], [3]) and [在]([j], [4])) or 
    ([在]([i], [4]) and [在]([j], [3])) or 
    ([在]([i], [4]) and [在]([j], [5])) or 
    ([在]([i], [5]) and [在]([j], [4]))) == [相邻]([i], [j]));
}

know{ //8,12
    forall [r] into [人] ([在]([r], [3]) == [喝]([r], [牛奶]));
    forall [r] into [人] ([抽]([r], [Blu]) == [喝]([r], [啤酒]));
}

know{ //10,11,15
    forall [i] into [人]
    forall [j] into [人]
    (([抽]([i], [Ble]) and [养]([j], [猫])) -> ([相邻]([i], [j])));

    forall [i] into [人]
    forall [j] into [人]
    (([抽]([i], [Dun]) and [养]([j], [马])) -> ([相邻]([i], [j])));

    forall [i] into [人]
    forall [j] into [人]
    (([抽]([i], [Ble]) and [喝]([j], [开水])) -> ([相邻]([i], [j])));
}

know{ //14
    [在]([挪威人], [1]) -> [在]([蓝房], [2]);
    [在]([挪威人], [2]) -> ([在]([蓝房], [3]) or [在]([蓝房], [1]));
    [在]([挪威人], [3]) -> ([在]([蓝房], [2]) or [在]([蓝房], [4]));
    [在]([挪威人], [4]) -> ([在]([蓝房], [3]) or [在]([蓝房], [5]));
    [在]([挪威人], [5]) -> [在]([蓝房], [4]);
}

>> [养]([挪威人], [猫]);
>> [养]([丹麦人], [马]);
>> [养]([英国人], [鸟]);
>> [养]([德国人], [鱼]);
>> [养]([瑞典人], [狗]);

>>'---------------------------------';

>> [在]([挪威人], [1]);
>> [在]([丹麦人], [2]);
>> [在]([英国人], [3]);
>> [在]([德国人], [4]);
>> [在]([瑞典人], [5]);

>>'---------------------------------';

>> [在]([黄房], [1]);
>> [在]([蓝房], [2]);
>> [在]([红房], [3]);
>> [在]([绿房], [4]);
>> [在]([白房], [5]);

>>'---------------------------------';

>> [喝]([挪威人], [开水]);
>> [喝]([丹麦人], [茶]);
>> [喝]([英国人], [牛奶]);
>> [喝]([德国人], [咖啡]);
>> [喝]([瑞典人], [啤酒]);

>>'---------------------------------';

>> [抽]([挪威人], [Dun]);
>> [抽]([丹麦人], [Ble]);
>> [抽]([英国人], [Pal]);
>> [抽]([德国人], [Pri]);
>> [抽]([瑞典人], [Blu]);